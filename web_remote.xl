// WebRemote module definition
//
// Control presentations from a web browser (computer, tablet, smartphone) 
//
// Copyright 2013 Taodyne SAS

module_description
    id "2EA5CF1D-5126-4966-ACF4-9916391F5689" 
    name "Web Remote Control"
    description "Control presentations from a web browser (computer, " &
                "tablet, smartphone)"
    import_name "WebRemote"
    author "Taodyne SAS"
    website "http://www.taodyne.com"
    version 1.0

module_description "fr",
    name "Télécommande Web"
    description "Télécommandez vos présentations depuis un navigateur web " &
                "(ordinateur, tablette, smartphone)"

// Desired port for the HTTP server. If not available, we try 10 ports
// in a row, and if we still can't find a port we request a dynamically
// allocated port form the system.
// The variable is set to the actual port used when the server is ready.
WEB_REMOTE_LOCAL_PORT -> 8000

// Set when server is ready
WEB_REMOTE_LOCAL_URL -> ""

import NodeJS

// #2618 module_dir is OK here but not if called from within web_remote_start
wrs_js_src := "var DOCUMENT_DIR='" & document_dir & "'; var HTTP_PORT=" & text WEB_REMOTE_LOCAL_PORT & "; require('" & module_dir & "/web_remote.js');"
wrs_count -> 0
web_remote_start ->
    locally
        nodejs wrs_js_src
    on "pagechange",
        nodejs_writeln "setCurrentPage(" & text page_number & ");"
    if wrs_count <= 2 then
        wrs_count := wrs_count + 1
        refresh 0
    if wrs_count = 2 then
        cached 1.0,
            for i in 1..page_count loop
                writeln "WebRemote: making thumbnail " & text i & "/" & text page_count
                save_page_thumbnail 160, 120, i, document_dir & "/thumbnails/page" & text i & ".png", 3.0
                nodejs_writeln "setPageName(" & text i & ", '" & page_name i & "');"
            // In case a client has connected before this point
            nodejs_writeln "sendPageNames();"
